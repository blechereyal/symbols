WHITESPACE                = _{ " " }
root_sym                  =  { ASCII_ALPHANUMERIC+ }
symbol_modifier_separator = _{ "." | ":" }
symbol_modifier           =  { ASCII_ALPHANUMERIC+ ~ symbol_modifier_separator }

// FUTURE
// Individual contract = [Symbol root][Month code][Year code].[Optional Extension]
// Continuous Contract Root Symbol = @[Symbol root].[Optional Extension]
// Continuous Contract Individual Symbol = @[Symbol root][Month code][Year code].[Optional Extension]
// TODO: Support this
// Custom Continuous Contract Parameters = @[Symbol root][Month code][Year code].[Optional Extension]=[Custom Continuous Parameters]

continuous_modifier = { "@" }
future_month        = { "F" | "G" | "H" | "J" | "K" | "M" | "N" | "Q" | "U" | "V" | "X" | "Z" }
year                = { ASCII_DIGIT{1, 2} }
individual_contract = { year ~ future_month ~ root_sym }
future_symbol       = { (symbol_modifier)? ~ ((individual_contract ~ continuous_modifier) | individual_contract | (root_sym ~ continuous_modifier)) }

// FUTURE OPTIONS = [Option root][Month code][Year code][Alias extension]<space>[Put/Call code][Strike Code]
// PUT and CALL spelt backwards
put = { "P" | "PUT" }
call = { "C" | "LLAC" }
put_call              = { put | call }
strike_code           = { ASCII_DIGIT+ }
future_options_symbol = { strike_code ~ put_call ~ (symbol_modifier)? ~ future_symbol }

// Stock Option = [Option Root]<space>[YYMMDD Expiration Date][C or P][Strike Price]
strike_price         = { ASCII_DIGIT* ~ "."? ~ ASCII_DIGIT+ }
date                 = { ASCII_DIGIT{6} }
stock_options_symbol = { strike_price ~ put_call ~ date ~ root_sym }

unknown_symbol = { symbol_modifier? ~ root_sym }

symbol = _{ SOI ~ (future_options_symbol | stock_options_symbol | future_symbol | unknown_symbol ) ~ EOI }
